" Chrome Cli bindings
"--------------------

" TODO: Falls apart when there are multiple windows of chrome open. Needs to
" be smartened up to handle the tab index format [34:123] for window:tab
function! s:OpenDevPageInChrome(refresh)
  if !executable('chrome-cli')
    echoerr 'OpenInChrome: `chrome-cli` executable required'
    return
  endif
  let domain = s:DomainForCurrentProject()
  let project_tab = s:TabForDomain(domain)
  if project_tab == ''
    call s:OpenTabForDomain(domain)
  else
    call s:FocusAndShowTab(project_tab, a:refresh)
  endif
  call s:ActivateChrome()
endfunction

function! s:TabForDomain(domain)
  let active_tab_url = system("chrome-cli info | grep -i '^url' | grep '".a:domain."\.dev'")
  if active_tab_url != ''
    let tab = system("chrome-cli info | grep -i '^id'")
    return matchlist(tab, '\v.*\s(.*)$')[1]
  else
    let tab = system("chrome-cli list links | grep '".a:domain."\.dev' | head -1")
    if tab != ''
      return matchlist(tab, '\v\[(\d+)\].*')[1]
    endif
  endif
endfunction

function! s:DomainForCurrentProject()
  return substitute(system('basename `pwd`'), '\n', '', '')
endfunction

function! s:OpenTabForDomain(domain)
  call system('chrome-cli open http://'.a:domain.'.dev')
endfunction

function! s:FocusAndShowTab(tab, refresh)
  call system('chrome-cli activate -t ' . a:tab)
  if a:refresh
    call system('chrome-cli reload')
  endif
endfunction

function! s:ActivateChrome()
  call system('osascript -e "tell application \"Google Chrome\" to activate"')
endfunction

command! OpenDevPageInChrome call <sid>OpenDevPageInChrome(0)
command! OpenAndReloadDevPage call <sid>OpenDevPageInChrome(1)
nnoremap gl :OpenAndReloadDevPage<cr>
nnoremap <F5> :OpenAndReloadDevPage<cr>

" vim:ft=vim
