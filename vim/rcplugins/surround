" Surround - Add, update, remove enclosing marks, ie [], {}. (), "", ''

Bundle "tpope/vim-surround"

function! SurroundRequote()
    let [single_quote, double_quote, matched_quote] = [1,2,0]
    let [column, line] = [virtcol('.'), getline('.')]
    let offset = 1
    while offset < 30
        let matched_quote = QuoteStyle(line, column, offset)
        if matched_quote
            call SwapSurroundingQuotes(matched_quote)
            break
        endif
        let offset += 1
    endwhile
    Pulse
endfunction

function! SwapSurroundingQuotes(current_quote)
    let [single_quote, double_quote] = [1,2]
    if a:current_quote != single_quote && a:current_quote != double_quote
        return
    endif
    call CacheCursorLocation()
    let quote_sequence = a:current_quote == single_quote ? "'\"" : "\"'"
    execute "normal cs" . quote_sequence
    call RestoreCursorLocation()
endfunction

function! QuoteStyle(line, column, offset)
    let [single_quote, double_quote, no_match] = [1,2,0]
    if a:line[a:column - a:offset - 1] == "'" || a:line[a:column + a:offset - 1] == "'"
        return single_quote
    elseif a:line[a:column - a:offset - 1] == '"' || a:line[a:column + a:offset - 1] == '"'
        return double_quote
    endif
    return no_match
endfunction

function! CacheCursorLocation()
    execute "normal mm"
endfunction

function! RestoreCursorLocation()
    execute "normal `m"
endfunction

nmap <leader>cs :call SurroundRequote()<cr>

" vim:ft=vim
