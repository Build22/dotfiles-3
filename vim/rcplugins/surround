" Surround - Add, update, remove enclosing marks, ie [], {}. (), "", ''

Bundle "tpope/vim-surround"

function! SurroundRequote()
    let [single_quote, double_quote, matched_quote] = [1,2,0]
    let [column, line] = [virtcol('.'), getline('.')]
    let offset = 1
    while offset < 30
        let left_index = column - offset
        let right_index = column + offset
        if left_index >= 0
            if line[left_index - 1] == '"'
                let matched_quote = double_quote
                break
            elseif line[left_index - 1] == "'"
                let matched_quote = single_quote
                break
            endif
        endif
        if line[right_index - 1] == '"'
            let matched_quote = double_quote
            break
        elseif line[right_index - 1] == "'"
            let matched_quote = single_quote
            break
        endif
        let offset += 1
    endwhile
    if matched_quote
        call SwapSurroundingQuotes(matched_quote)
    endif
endfunction

function! SwapSurroundingQuotes(current_quote)
    let [single_quote, double_quote] = [1,2]
    if a:current_quote != single_quote && a:current_quote != double_quote
        return
    endif
    call CacheCursorLocation()
    let quote_sequence = a:current_quote == single_quote ? "'\"" : "\"'"
    execute "normal cs" . quote_sequence
    call RestoreCursorLocation()
endfunction

function! CacheCursorLocation()
    execute "normal mm"
endfunction

function! RestoreCursorLocation()
    execute "normal `m"
endfunction

nmap <leader>cs :call SurroundRequote()<cr>

" vim:ft=vim
